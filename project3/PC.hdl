// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */

/**
 * key insight:
 * - to represent the order of conditions, the muxes are in reverse order
 * - load= true since we control the value in the mux chain where load is handled
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    Inc16(in=tmpOut, out=rInc);

    Mux16(a=tmpOut, b=rInc, sel=inc, out=tmp1);
    Mux16(a=tmp1, b=in, sel=load, out=tmp2);
    Mux16(a=tmp2, b=false, sel=reset, out=tmp3);

    Register(in=tmp3, load=true, out=out, out=tmpOut);
}